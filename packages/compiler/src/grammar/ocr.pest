/* 
 * PROGRAM MATCHING
 */
//
program = _{SOI ~ block ~ NEWLINE* ~ EOI}
block = !{ line ~ (NEWLINE ~ line)* }
line = _{ NEWLINE* ~ statement }

statement = _{ 
	(
	comment | 
	assignment |
	forLoop |
	ifStatement | 
	expression
	) ~ WHITESPACE*
}
///

/*
 * COMMENTS
 */
//
commentDelimeter = _{ "//"}
comment = { commentDelimeter ~ (!(NEWLINE) ~ ANY)* }
///

/*
 * ASSIGNMENTS & DECLARATIONS
 */
//
assignment = {
	arrayDeclaration | 
	arrayAssignment | 
	taggedVariableAssignment | 
	variableAssignment
}

variableAssignment = !{
	identifier ~ assignmentOperator ~ expression
}

taggedVariableAssignment = ${
	declarationKeywords ~ WHITESPACE+ ~ variableAssignment
}
///

/*
 * ARRAYS
 */
//
arrayValue = _{ arrayValues | expression }
arrayValues = !{ "[" ~ (arrayValue ~ ",")* ~ arrayValue ~ "]" }
arrayEmptyValues = { "[" ~ "]" }
array = !{ arrayValues | arrayEmptyValues }
arrayIndexing = ${ identifier ~ arrayValues }
arraySetting = ${
	WHITESPACE* ~ assignmentOperator ~ WHITESPACE*
}
arrayDeclaration = ${
	arrayKeyword ~ WHITESPACE+ ~ arrayIndexing ~ (arraySetting ~ array)?
}
arrayAssignment = ${ 
	arrayIndexing ~ arraySetting ~ expression
}
///

/* 
 * EXPRESSIONS
 */
//
expression = ${
	term ~ (WHITESPACE* ~ operator ~ WHITESPACE* ~ term)*
}
term = _{
	termTypes | "(" ~ expression ~ ")"
}
termTypes = _{
	arrayIndexing | array | functionCall | identifier | literal
}
///

/*
 * FUNCTION CALLS
 */
//
functionCall = ${
	identifier ~ functionParameters
}
functionParameters = !{
	"(" ~ ((expression ~ ",")* ~ expression)* ~ ")"
}
///

/* 
 * DATA TYPES
 */
//
literal = ${
	(
	number | 
	boolean |
	null |
	string
	) ~ !identifierCharacters
}

boolean = { "true" | "false" }
null = { "null" }

number = _{ negativeNumber | positiveNumber}
positiveNumber = @{ "+"? ~ numberValue }
negativeNumber = @{ "-" ~ numberValue }
numberValue = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)? }

// String parsing, taken from the pest.rs book
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
///

/*
 * IDENTIFIERS 
 */ 
//
identifierCharacters = _{ ASCII_ALPHANUMERIC | "_" }
identifier = @{ !keywords ~ (ASCII_ALPHA | "_") ~ identifierCharacters* }
///

/* 
 * KEYWORDS 
 */
//
keywords = @{
	(
	declarationKeywords |
	arrayKeywords |
	forKeywords |
	ifKeywords |
	wordOperators 
	) ~ !identifierCharacters
}

declarationKeywords = _{ constKeyword | globalKeyword }
constKeyword = { "const" }
globalKeyword = { "global" }

arrayKeywords = { arrayKeyword }
arrayKeyword = { "array" }

forKeywords = _{ forKeyword | toKeyword | nextKeyword | stepKeyword }
forKeyword = _{ "for" }
toKeyword = _{ "to" }
nextKeyword = _{ "next" }
stepKeyword = _{ "step" }

ifKeywords = _{ ifKeyword | thenKeyword | elseIfKeyword | elseKeyword | endIfKeyword }
ifKeyword = _{ "if" }
thenKeyword = _{ "then" }
elseIfKeyword = _{ "elseif" }
elseKeyword = _{ "else" }
endIfKeyword = _{ "endif" }
///

/* 
 * OPERATORS 
 */
//
operator = ${
	characterOperators |
	wordOperators ~ WHITESPACE+
}

characterOperators = _{
	additionOperator | 
	subtractionOperator |
	multiplicationOperator |
	divisionOperator |
	exponentOperator |
	equalToOperator |
	notEqualToOperator |
	lessThanEqualToOperator |
	greaterThanEqualToOperator |
	lessThanOperator |
	greaterThanOperator
}

wordOperators = _{
	modulusOperator |
	quotientOperator |
	logicalAndOperator |
	logicalOrOperator |
	logicalNotOperator
}

additionOperator = !{ "+" }
subtractionOperator = !{ "-" }
multiplicationOperator = { "*" }
divisionOperator = !{ "/" }
exponentOperator = !{ "^" }

equalToOperator = !{ "==" }
notEqualToOperator = !{ "!=" }
lessThanOperator = !{ "<" }
lessThanEqualToOperator = !{ "<=" }
greaterThanOperator = !{ ">" }
greaterThanEqualToOperator = !{ ">=" }

modulusOperator = !{ "MOD" }
quotientOperator = !{ "DIV" }
logicalAndOperator = !{ "AND" }
logicalOrOperator = !{ "OR" }
logicalNotOperator = !{ "NOT" }

assignmentOperator = _{ "=" }
///

/*
 * FOR LOOPS
 */
//
forLoop = ${
	forKeyword ~ 
	WHITESPACE+ ~
	forLoopAssignment ~
	WHITESPACE+ ~
	toKeyword ~ 
	WHITESPACE+ ~
	expression ~
	WHITESPACE* ~
	NEWLINE+ ~
	block ~
	NEWLINE+ ~
	WHITESPACE* ~
	nextKeyword ~
	WHITESPACE+ ~
	POP
}

forLoopAssignment = !{
	PUSH(identifier) ~ assignmentOperator ~ expression
}
///

/*
 * IF STATEMENT
 */
//
ifStatement = ${
	ifKeyword ~ 
	WHITESPACE+ ~
	expression ~
	WHITESPACE+ ~
	thenKeyword ~
	WHITESPACE* ~
	NEWLINE+ ~
	block ~
	elseIfStatement* ~
	elseStatement? ~
	NEWLINE+ ~
	WHITESPACE* ~
	endIfKeyword
}

elseIfStatement = {
	NEWLINE+ ~
	WHITESPACE* ~
	elseIfKeyword ~ 
	WHITESPACE+ ~
	expression ~
	WHITESPACE+ ~
	thenKeyword ~
	WHITESPACE* ~
	NEWLINE+ ~
	block
}

elseStatement = {
	NEWLINE+ ~
	WHITESPACE* ~
	elseKeyword ~
	WHITESPACE* ~
	NEWLINE+ ~
	block
}
///

/* 
 * MISC 
 */
//
WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
///